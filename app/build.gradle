plugins {
    id 'com.android.application'
}
ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 21
ext.versionClassifier = null
ext.isSnapshot = true
ext.minimumSdkVersion = 28

android {
    namespace 'mz.org.csaude.mentoring'
    compileSdk 34

    buildFeatures {
        dataBinding = true
        viewBinding true
    }

    defaultConfig {
        applicationId "mz.org.csaude.mentoring"
        minSdk 28
        targetSdk 34
        versionCode generateVersionCode()
        versionName generateVersionName()
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
        coreLibraryDesugaringEnabled true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Mentoria-${variant.versionName}.apk"
        }
    }
}


private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    } else {
        ext.versionClassifier = "RELEASE"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.4"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.navigation:navigation-fragment:2.6.0'
    implementation 'androidx.navigation:navigation-ui:2.6.0'
    implementation 'androidx.annotation:annotation:1.6.0'
    implementation 'androidx.activity:activity:1.8.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'androidx.security:security-crypto:1.0.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

    // Libraries
    implementation 'androidx.work:work-runtime:2.8.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    implementation 'com.jakewharton:butterknife:10.2.3'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "androidx.concurrent:concurrent-futures:1.1.0"
    implementation 'com.j256.ormlite:ormlite-android:5.0'
    implementation 'com.ogaclejapan.smarttablayout:library:2.0.0@aar'
    implementation 'com.ogaclejapan.smarttablayout:utils-v4:2.0.0@aar'
    runtimeOnly 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    runtimeOnly 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'com.j256.ormlite:ormlite-core:5.2'
    implementation 'joda-time:joda-time:2.9.6'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.itextpdf:itext7-core:7.2.3'
    implementation 'com.android.support:multidex:1.0.3'
    implementation "androidx.room:room-runtime:2.6.1"
    annotationProcessor "androidx.room:room-compiler:2.6.1"
    implementation "net.zetetic:android-database-sqlcipher:4.5.0"
    implementation 'androidx.preference:preference:1.1.1'
    implementation "androidx.paging:paging-runtime:3.1.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"
    implementation "androidx.biometric:biometric:1.2.0-alpha05"


}